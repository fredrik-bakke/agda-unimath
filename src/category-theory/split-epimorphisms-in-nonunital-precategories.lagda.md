# Split epimorphisms in nonunital precategories

```agda
module category-theory.split-epimorphisms-in-nonunital-precategories where
```

<details><summary>Imports</summary>

```agda
open import category-theory.nonunital-precategories

open import foundation.action-on-identifications-functions
open import foundation.cartesian-product-types
open import foundation.dependent-pair-types
open import foundation.equivalences
open import foundation.function-types
open import foundation.homotopies
open import foundation.identity-types
open import foundation.injective-maps
open import foundation.propositions
open import foundation.retractions
open import foundation.sections
open import foundation.sets
open import foundation.subtypes
open import foundation.universe-levels
```

</details>

## Idea

A
{{#concept "split epimorphism" Disambiguation="in a nonunital precategory" Agda=split-epi-Nonunital-Precategory}}
in a [nonunital precategory](category-theory.nonunital-precategory.md) `ğ’` is a
morphism `f : x â†’ y` in `ğ’` such that there is a family of left inverse maps to
precomposition

```text
  g z : hom(x, z) â†’ hom(y, z)
```

such that

```text
  g z âˆ˜ hom(f, z) ~ id
```

and a family of right inverse maps to postcomposition

```text
  h z : hom(z, y) â†’ hom(z, x)
```

such that

```text
  hom(z, f) âˆ˜ h z ~ id
```

for all objects `z` in `ğ’`.

Note that unlike the predicate of being a split epimorphism in a unital
[precategory](category-theory.precategories.md), the
[proposition](foundation-core.propositions.md) considered here is large with
respect to the collection of objects.

## Definitions

### The predicate of being a split epimorphism in a nonunital precategory

```agda
module _
  {l1 l2 : Level}
  (ğ’ : Nonunital-Precategory l1 l2)
  {x y : obj-Nonunital-Precategory ğ’}
  (f : hom-Nonunital-Precategory ğ’ x y)
  where

  is-split-epi-Nonunital-Precategory : UU (l1 âŠ” l2)
  is-split-epi-Nonunital-Precategory =
    ( (z : obj-Nonunital-Precategory ğ’) â†’
      retraction (precomp-hom-Nonunital-Precategory ğ’ f z)) Ã—
    ( (z : obj-Nonunital-Precategory ğ’) â†’
      section (postcomp-hom-Nonunital-Precategory ğ’ f z))

  is-set-is-split-epi-Nonunital-Precategory :
    is-set is-split-epi-Nonunital-Precategory
  is-set-is-split-epi-Nonunital-Precategory =
    is-set-product
      ( is-set-Î 
        ( Î» z â†’
          is-set-Î£
            ( is-set-function-type (is-set-hom-Nonunital-Precategory ğ’ y z))
            ( Î» g â†’
              is-set-is-prop
                ( is-prop-Î 
                  ( Î» h â†’
                    is-set-hom-Nonunital-Precategory ğ’ y z
                      ( g (comp-hom-Nonunital-Precategory ğ’ h f))
                      ( h))))))
      ( is-set-Î 
        ( Î» z â†’
          is-set-Î£
            ( is-set-function-type (is-set-hom-Nonunital-Precategory ğ’ z x))
            ( Î» g â†’
              is-set-is-prop
                ( is-prop-Î 
                  ( Î» h â†’
                    is-set-hom-Nonunital-Precategory ğ’ z y
                      ( comp-hom-Nonunital-Precategory ğ’ f (g h))
                      ( h))))))
```

We observe that the previous argument generalizes to conclude that if the
hom-types are ğ‘›-truncated, then the type of splittings is too.

```agda
module _
  {l1 l2 : Level}
  (ğ’ : Nonunital-Precategory l1 l2)
  {x y : obj-Nonunital-Precategory ğ’}
  {f : hom-Nonunital-Precategory ğ’ x y}
  (H : is-split-epi-Nonunital-Precategory ğ’ f)
  where

  retraction-precomp-is-split-epi-Nonunital-Precategory :
    (z : obj-Nonunital-Precategory ğ’) â†’
    retraction (precomp-hom-Nonunital-Precategory ğ’ f z)
  retraction-precomp-is-split-epi-Nonunital-Precategory = pr1 H

  map-retraction-precomp-is-split-epi-Nonunital-Precategory :
    (z : obj-Nonunital-Precategory ğ’) â†’
    hom-Nonunital-Precategory ğ’ x z â†’ hom-Nonunital-Precategory ğ’ y z
  map-retraction-precomp-is-split-epi-Nonunital-Precategory z =
    map-retraction
      ( precomp-hom-Nonunital-Precategory ğ’ f z)
      ( retraction-precomp-is-split-epi-Nonunital-Precategory z)

  section-postcomp-is-split-epi-Nonunital-Precategory :
    (z : obj-Nonunital-Precategory ğ’) â†’
    section (postcomp-hom-Nonunital-Precategory ğ’ f z)
  section-postcomp-is-split-epi-Nonunital-Precategory = pr2 H

  map-section-postcomp-is-split-epi-Nonunital-Precategory :
    (z : obj-Nonunital-Precategory ğ’) â†’
    hom-Nonunital-Precategory ğ’ z y â†’ hom-Nonunital-Precategory ğ’ z x
  map-section-postcomp-is-split-epi-Nonunital-Precategory z =
    map-section
      ( postcomp-hom-Nonunital-Precategory ğ’ f z)
      ( section-postcomp-is-split-epi-Nonunital-Precategory z)
```

### The set of split epimorphisms between two objects in a nonunital precategory

```agda
module _
  {l1 l2 : Level}
  (ğ’ : Nonunital-Precategory l1 l2)
  (x y : obj-Nonunital-Precategory ğ’)
  where

  split-epi-Nonunital-Precategory : UU (l1 âŠ” l2)
  split-epi-Nonunital-Precategory =
    Î£ (hom-Nonunital-Precategory ğ’ x y) (is-split-epi-Nonunital-Precategory ğ’)

  is-set-split-epi-Nonunital-Precategory :
    is-set split-epi-Nonunital-Precategory
  is-set-split-epi-Nonunital-Precategory =
    is-set-Î£
      ( is-set-hom-Nonunital-Precategory ğ’ x y)
      ( is-set-is-split-epi-Nonunital-Precategory ğ’)

  split-epi-set-Nonunital-Precategory : Set (l1 âŠ” l2)
  split-epi-set-Nonunital-Precategory =
    ( split-epi-Nonunital-Precategory ,
      is-set-split-epi-Nonunital-Precategory)

module _
  {l1 l2 : Level}
  (ğ’ : Nonunital-Precategory l1 l2)
  {x y : obj-Nonunital-Precategory ğ’}
  (f : split-epi-Nonunital-Precategory ğ’ x y)
  where

  hom-split-epi-Nonunital-Precategory : hom-Nonunital-Precategory ğ’ x y
  hom-split-epi-Nonunital-Precategory = pr1 f

  is-split-epi-split-epi-Nonunital-Precategory :
    is-split-epi-Nonunital-Precategory ğ’ hom-split-epi-Nonunital-Precategory
  is-split-epi-split-epi-Nonunital-Precategory = pr2 f
```

## Properties

### Composition of split epimorphisms

```agda
module _
  {l1 l2 : Level}
  (ğ’ : Nonunital-Precategory l1 l2)
  {x y z : obj-Nonunital-Precategory ğ’}
  (g : hom-Nonunital-Precategory ğ’ y z)
  (f : hom-Nonunital-Precategory ğ’ x y)
  (G : is-split-epi-Nonunital-Precategory ğ’ g)
  (F : is-split-epi-Nonunital-Precategory ğ’ f)
  where

  is-split-epi-comp-hom-Nonunital-Precategory :
    is-split-epi-Nonunital-Precategory ğ’ (comp-hom-Nonunital-Precategory ğ’ g f)
  pr1 is-split-epi-comp-hom-Nonunital-Precategory z =
    retraction-left-map-triangle'
      ( precomp-hom-Nonunital-Precategory ğ’
        ( comp-hom-Nonunital-Precategory ğ’ g f)
        ( z))
      ( precomp-hom-Nonunital-Precategory ğ’ f z)
      ( precomp-hom-Nonunital-Precategory ğ’ g z)
      ( Î» h â†’ associative-comp-hom-Nonunital-Precategory ğ’ h g f)
      ( retraction-precomp-is-split-epi-Nonunital-Precategory ğ’ F z)
      ( retraction-precomp-is-split-epi-Nonunital-Precategory ğ’ G z)
  pr2 is-split-epi-comp-hom-Nonunital-Precategory z =
    section-left-map-triangle
      ( postcomp-hom-Nonunital-Precategory ğ’
        ( comp-hom-Nonunital-Precategory ğ’ g f)
        ( z))
      ( postcomp-hom-Nonunital-Precategory ğ’ g z)
      ( postcomp-hom-Nonunital-Precategory ğ’ f z)
      ( associative-comp-hom-Nonunital-Precategory ğ’ g f)
      ( section-postcomp-is-split-epi-Nonunital-Precategory ğ’ F z)
      ( section-postcomp-is-split-epi-Nonunital-Precategory ğ’ G z)

module _
  {l1 l2 : Level}
  (ğ’ : Nonunital-Precategory l1 l2)
  {x y z : obj-Nonunital-Precategory ğ’}
  (g : split-epi-Nonunital-Precategory ğ’ y z)
  (f : split-epi-Nonunital-Precategory ğ’ x y)
  where

  comp-split-epi-Nonunital-Precategory :
    split-epi-Nonunital-Precategory ğ’ x z
  comp-split-epi-Nonunital-Precategory =
    ( comp-hom-Nonunital-Precategory ğ’
      ( hom-split-epi-Nonunital-Precategory ğ’ g)
      ( hom-split-epi-Nonunital-Precategory ğ’ f)) ,
    ( is-split-epi-comp-hom-Nonunital-Precategory ğ’
      ( hom-split-epi-Nonunital-Precategory ğ’ g)
      ( hom-split-epi-Nonunital-Precategory ğ’ f)
      ( is-split-epi-split-epi-Nonunital-Precategory ğ’ g)
      ( is-split-epi-split-epi-Nonunital-Precategory ğ’ f))
```

### Right cancellation of split epimorphisms

```agda
module _
  {l1 l2 : Level}
  (ğ’ : Nonunital-Precategory l1 l2)
  {x y z : obj-Nonunital-Precategory ğ’}
  (g : hom-Nonunital-Precategory ğ’ y z)
  (f : hom-Nonunital-Precategory ğ’ x y)
  (GF :
    is-split-epi-Nonunital-Precategory ğ’ (comp-hom-Nonunital-Precategory ğ’ g f))
  where

  is-split-epi-left-factor-Nonunital-Precategory :
    is-split-epi-Nonunital-Precategory ğ’ g
  pr1 is-split-epi-left-factor-Nonunital-Precategory z =
    retraction-top-map-triangle'
      ( precomp-hom-Nonunital-Precategory ğ’
        ( comp-hom-Nonunital-Precategory ğ’ g f)
        ( z))
      ( precomp-hom-Nonunital-Precategory ğ’ f z)
      ( precomp-hom-Nonunital-Precategory ğ’ g z)
      ( Î» h â†’ associative-comp-hom-Nonunital-Precategory ğ’ h g f)
      ( retraction-precomp-is-split-epi-Nonunital-Precategory ğ’ GF z)
  pr2 is-split-epi-left-factor-Nonunital-Precategory z =
    section-right-map-triangle
      ( postcomp-hom-Nonunital-Precategory ğ’
        ( comp-hom-Nonunital-Precategory ğ’ g f)
        ( z))
      ( postcomp-hom-Nonunital-Precategory ğ’ g z)
      ( postcomp-hom-Nonunital-Precategory ğ’ f z)
      ( associative-comp-hom-Nonunital-Precategory ğ’ g f)
      ( section-postcomp-is-split-epi-Nonunital-Precategory ğ’ GF z)
```
