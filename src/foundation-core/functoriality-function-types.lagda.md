# Functoriality of function types

```agda
module foundation-core.functoriality-function-types where
```

<details><summary>Imports</summary>

```agda
open import foundation.action-on-identifications-functions
open import foundation.dependent-pair-types
open import foundation.function-extensionality
open import foundation.type-theoretic-principle-of-choice
open import foundation.universe-levels

open import foundation-core.contractible-maps
open import foundation-core.contractible-types
open import foundation-core.equivalences
open import foundation-core.fibers-of-maps
open import foundation-core.function-types
open import foundation-core.functoriality-dependent-function-types
open import foundation-core.functoriality-dependent-pair-types
open import foundation-core.homotopies
open import foundation-core.identity-types
open import foundation-core.propositions
open import foundation-core.sets
open import foundation-core.truncated-types
open import foundation-core.truncation-levels
open import foundation-core.whiskering-homotopies
```

</details>

## Properties

### Postcomposition preserves homotopies

```agda
htpy-postcomp :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (A : UU l3) ‚Üí
  {f g : X ‚Üí Y} ‚Üí (f ~ g) ‚Üí postcomp A f ~ postcomp A g
htpy-postcomp A H h = eq-htpy (H ‚àò h)

compute-htpy-postcomp-refl-htpy :
  {l1 l2 l3 : Level} (A : UU l1) {B : UU l2} {C : UU l3} (f : B ‚Üí C) ‚Üí
  (htpy-postcomp A (refl-htpy' f)) ~ refl-htpy
compute-htpy-postcomp-refl-htpy A f h = eq-htpy-refl-htpy (f ‚àò h)
```

### The fibers of `postcomp`

```agda
compute-fiber-postcomp :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (A : UU l3) ‚Üí
  (f : X ‚Üí Y) (h : A ‚Üí Y) ‚Üí
  ((x : A) ‚Üí fiber f (h x)) ‚âÉ fiber (postcomp A f) h
compute-fiber-postcomp A f h =
  equiv-tot (Œª _ ‚Üí equiv-eq-htpy) ‚àòe distributive-Œ†-Œ£
```

### Precomposition preserves homotopies

```agda
htpy-precomp :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {f g : A ‚Üí B} (H : f ~ g) (C : UU l3) ‚Üí
  (precomp f C) ~ (precomp g C)
htpy-precomp H C h = eq-htpy (h ¬∑l H)

compute-htpy-precomp-refl-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B) (C : UU l3) ‚Üí
  (htpy-precomp (refl-htpy' f) C) ~ refl-htpy
compute-htpy-precomp-refl-htpy f C h = eq-htpy-refl-htpy (h ‚àò f)
```

### Postcomposition and equivalences

#### A map `f` is an equivalence if and only if postcomposing by `f` is an equivalence

```agda
module _
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X ‚Üí Y)
  (H : {l3 : Level} (A : UU l3) ‚Üí is-equiv (postcomp A f))
  where

  map-inv-is-equiv-is-equiv-postcomp : Y ‚Üí X
  map-inv-is-equiv-is-equiv-postcomp = map-inv-is-equiv (H Y) id

  is-section-map-inv-is-equiv-is-equiv-postcomp :
    ( f ‚àò map-inv-is-equiv-is-equiv-postcomp) ~ id
  is-section-map-inv-is-equiv-is-equiv-postcomp =
    htpy-eq (is-section-map-inv-is-equiv (H Y) id)

  is-retraction-map-inv-is-equiv-is-equiv-postcomp :
    ( map-inv-is-equiv-is-equiv-postcomp ‚àò f) ~ id
  is-retraction-map-inv-is-equiv-is-equiv-postcomp =
    htpy-eq
      ( ap
        ( pr1)
        ( eq-is-contr
          ( is-contr-map-is-equiv (H X) f)
          { x =
            pair
              ( map-inv-is-equiv-is-equiv-postcomp ‚àò f)
              ( ap (Œª u ‚Üí u ‚àò f) (is-section-map-inv-is-equiv (H Y) id))}
          { y = pair id refl}))

  abstract
    is-equiv-is-equiv-postcomp : is-equiv f
    is-equiv-is-equiv-postcomp =
      is-equiv-is-invertible
        map-inv-is-equiv-is-equiv-postcomp
        is-section-map-inv-is-equiv-is-equiv-postcomp
        is-retraction-map-inv-is-equiv-is-equiv-postcomp
```

The following version of the same theorem works when `X` and `Y` are in the same
universe. The condition of inducing equivalences by postcomposition is
simplified to that universe.

```agda
is-equiv-is-equiv-postcomp' :
  {l : Level} {X : UU l} {Y : UU l} (f : X ‚Üí Y) ‚Üí
  ((A : UU l) ‚Üí is-equiv (postcomp A f)) ‚Üí is-equiv f
is-equiv-is-equiv-postcomp'
  {l} {X} {Y} f is-equiv-postcomp-f =
  let section-f = center (is-contr-map-is-equiv (is-equiv-postcomp-f Y) id)
  in
  is-equiv-is-invertible
    ( pr1 section-f)
    ( htpy-eq (pr2 section-f))
    ( htpy-eq
      ( ap
        ( pr1)
        ( eq-is-contr'
          ( is-contr-map-is-equiv (is-equiv-postcomp-f X) f)
          ( pair ((pr1 section-f) ‚àò f) (ap (Œª t ‚Üí t ‚àò f) (pr2 section-f)))
          ( pair id refl))))

abstract
  is-equiv-postcomp-is-equiv :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X ‚Üí Y) ‚Üí is-equiv f ‚Üí
    {l3 : Level} (A : UU l3) ‚Üí is-equiv (postcomp A f)
  is-equiv-postcomp-is-equiv {X = X} {Y = Y} f is-equiv-f A =
    is-equiv-is-invertible
      ( postcomp A (map-inv-is-equiv is-equiv-f))
      ( eq-htpy ‚àò htpy-right-whisk (is-section-map-inv-is-equiv is-equiv-f))
      ( eq-htpy ‚àò htpy-right-whisk (is-retraction-map-inv-is-equiv is-equiv-f))

  is-equiv-postcomp-equiv :
    {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X ‚âÉ Y) ‚Üí
    {l3 : Level} (A : UU l3) ‚Üí is-equiv (postcomp A (map-equiv f))
  is-equiv-postcomp-equiv f =
    is-equiv-postcomp-is-equiv (map-equiv f) (is-equiv-map-equiv f)

equiv-postcomp :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (A : UU l3) ‚Üí
  (X ‚âÉ Y) ‚Üí (A ‚Üí X) ‚âÉ (A ‚Üí Y)
pr1 (equiv-postcomp A e) = postcomp A (map-equiv e)
pr2 (equiv-postcomp A e) =
  is-equiv-postcomp-is-equiv (map-equiv e) (is-equiv-map-equiv e) A
```

### Precomposition and equivalences

#### If dependent precomposition by `f` is an equivalence, then precomposition by `f` is an equivalence

```agda
abstract
  is-equiv-precomp-is-equiv-precomp-Œ† :
    {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B) ‚Üí
    ((C : B ‚Üí UU l3) ‚Üí is-equiv (precomp-Œ† f C)) ‚Üí
    ((C : UU l3) ‚Üí is-equiv (precomp f C))
  is-equiv-precomp-is-equiv-precomp-Œ† f is-equiv-precomp-Œ†-f C =
    is-equiv-precomp-Œ†-f (Œª y ‚Üí C)
```

#### If `f` is an equivalence, then precomposition by `f` is an equivalence

```agda
abstract
  is-equiv-precomp-is-equiv :
    {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A ‚Üí B) ‚Üí is-equiv f ‚Üí
    (C : UU l3) ‚Üí is-equiv (precomp f C)
  is-equiv-precomp-is-equiv f is-equiv-f =
    is-equiv-precomp-is-equiv-precomp-Œ† f
      ( is-equiv-precomp-Œ†-is-equiv is-equiv-f)

  is-equiv-precomp-equiv :
    {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A ‚âÉ B) ‚Üí
    (C : UU l3) ‚Üí is-equiv (precomp (map-equiv f) C)
  is-equiv-precomp-equiv f =
    is-equiv-precomp-is-equiv (map-equiv f) (is-equiv-map-equiv f)

equiv-precomp :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (e : A ‚âÉ B) (C : UU l3) ‚Üí
  (B ‚Üí C) ‚âÉ (A ‚Üí C)
pr1 (equiv-precomp e C) = precomp (map-equiv e) C
pr2 (equiv-precomp e C) =
  is-equiv-precomp-is-equiv (map-equiv e) (is-equiv-map-equiv e) C
```

#### If precomposing by `f` is an equivalence, then `f` is an equivalence

First, we prove this relative to a subuniverse, such that `f` is a map between
two types in that subuniverse.

```agda
module _
  { l1 l2 : Level}
  ( Œ± : Level ‚Üí Level) (P : (l : Level) ‚Üí UU l ‚Üí UU (Œ± l))
  ( A : Œ£ (UU l1) (P l1)) (B : Œ£ (UU l2) (P l2)) (f : pr1 A ‚Üí pr1 B)
  ( H : (l : Level) (C : Œ£ (UU l) (P l)) ‚Üí is-equiv (precomp f (pr1 C)))
  where

  map-inv-is-equiv-precomp-subuniverse : pr1 B ‚Üí pr1 A
  map-inv-is-equiv-precomp-subuniverse =
    pr1 (center (is-contr-map-is-equiv (H _ A) id))

  is-section-map-inv-is-equiv-precomp-subuniverse :
    ( f ‚àò map-inv-is-equiv-precomp-subuniverse) ~ id
  is-section-map-inv-is-equiv-precomp-subuniverse =
    htpy-eq
      ( ap
        ( pr1)
        ( eq-is-contr'
          ( is-contr-map-is-equiv (H _ B) f)
          ( ( f ‚àò (pr1 (center (is-contr-map-is-equiv (H _ A) id)))) ,
            ( ap
              ( Œª (g : pr1 A ‚Üí pr1 A) ‚Üí f ‚àò g)
              ( pr2 (center (is-contr-map-is-equiv (H _ A) id)))))
          ( id , refl)))

  is-retraction-map-inv-is-equiv-precomp-subuniverse :
    ( map-inv-is-equiv-precomp-subuniverse ‚àò f) ~ id
  is-retraction-map-inv-is-equiv-precomp-subuniverse =
    htpy-eq (pr2 (center (is-contr-map-is-equiv (H _ A) id)))

  abstract
    is-equiv-is-equiv-precomp-subuniverse :
      is-equiv f
    is-equiv-is-equiv-precomp-subuniverse =
      is-equiv-is-invertible
        ( map-inv-is-equiv-precomp-subuniverse)
        ( is-section-map-inv-is-equiv-precomp-subuniverse)
        ( is-retraction-map-inv-is-equiv-precomp-subuniverse)
```

Now we prove the usual statement, without the subuniverse

```agda
module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  abstract
    is-equiv-is-equiv-precomp :
      (f : A ‚Üí B) ‚Üí ((l : Level) (C : UU l) ‚Üí is-equiv (precomp f C)) ‚Üí
      is-equiv f
    is-equiv-is-equiv-precomp f is-equiv-precomp-f =
      is-equiv-is-equiv-precomp-subuniverse
        ( Œª l ‚Üí l1 ‚äî l2)
        ( Œª l X ‚Üí A ‚Üí B)
        ( A , f)
        ( B , f)
        ( f)
        ( Œª l C ‚Üí is-equiv-precomp-f l (pr1 C))
```

#### Corollaries for particular subuniverses

```agda
is-equiv-is-equiv-precomp-Prop :
  {l1 l2 : Level} (P : Prop l1) (Q : Prop l2)
  (f : type-Prop P ‚Üí type-Prop Q) ‚Üí
  ({l : Level} (R : Prop l) ‚Üí is-equiv (precomp f (type-Prop R))) ‚Üí
  is-equiv f
is-equiv-is-equiv-precomp-Prop P Q f H =
  is-equiv-is-equiv-precomp-subuniverse id (Œª l ‚Üí is-prop) P Q f (Œª l ‚Üí H {l})

is-equiv-is-equiv-precomp-Set :
  {l1 l2 : Level} (A : Set l1) (B : Set l2)
  (f : type-Set A ‚Üí type-Set B) ‚Üí
  ({l : Level} (C : Set l) ‚Üí is-equiv (precomp f (type-Set C))) ‚Üí
  is-equiv f
is-equiv-is-equiv-precomp-Set A B f H =
  is-equiv-is-equiv-precomp-subuniverse id (Œª l ‚Üí is-set) A B f (Œª l ‚Üí H {l})

is-equiv-is-equiv-precomp-Truncated-Type :
  {l1 l2 : Level} (k : ùïã)
  (A : Truncated-Type l1 k) (B : Truncated-Type l2 k)
  (f : type-Truncated-Type A ‚Üí type-Truncated-Type B) ‚Üí
  ({l : Level} (C : Truncated-Type l k) ‚Üí is-equiv (precomp f (pr1 C))) ‚Üí
  is-equiv f
is-equiv-is-equiv-precomp-Truncated-Type k A B f H =
    is-equiv-is-equiv-precomp-subuniverse id (Œª l ‚Üí is-trunc k) A B f
      ( Œª l ‚Üí H {l})
```

## See also

- Functorial properties of dependent function types are recorded in
  [`foundation.functoriality-dependent-function-types`](foundation.functoriality-dependent-function-types.md).
- Arithmetical laws involving dependent function types are recorded in
  [`foundation.type-arithmetic-dependent-function-types`](foundation.type-arithmetic-dependent-function-types.md).
- Equality proofs in dependent function types are characterized in
  [`foundation.equality-dependent-function-types`](foundation.equality-dependent-function-types.md).
